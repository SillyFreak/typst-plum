use std::collections::BTreeMap;
use std::str::FromStr;

use either::Either;
use lalrpop_util::ParseError;

use crate::model::*;

grammar;

pub Diagram: Diagram<'input> = {
    "\n"* <mut items: (<ClassifierOrEdge> "\n"+)*> <item: ClassifierOrEdge?> => {
        items.extend(item);
        let mut classifiers = Vec::new();
        let mut edges = Vec::new();
        for item in items {
            match item {
                Either::Left(item) => classifiers.push(item),
                Either::Right(item) => edges.push(item),
            }
        }
        Diagram { classifiers, edges }
    }
}

ClassifierOrEdge: Either<Classifier<'input>, Edge<'input>> = {
    Classifier => Either::Left(<>),
    Edge => Either::Right(<>),
}

Classifier: Classifier<'input> = {
    <meta: Metas>
    <is_abstract: "abstract"?> <is_final: "final"?> <kind: ClassifierKind> <name: Name> <id: ("as" <Name>)?> <body: ClassifierBody> => {
        let (kind, stereotype) = kind;
        let is_abstract = is_abstract.is_some() || kind == ClassifierKind::Interface;
        let is_final = is_final.is_some();
        let mut stereotypes = Vec::new();
        stereotypes.extend(stereotype);
        let attributes = body;
        Classifier { meta, is_abstract, is_final, kind, name, id, stereotypes, attributes }
    }
}

ClassifierBody: Vec<Attribute<'input>> = {
    => Vec::new(),
    "{" "\n"* "}" => Vec::new(),
    "{" "\n"+ <attrs: (<Attribute> "\n"+)+> "}" => attrs,
}

Attribute: Attribute<'input> = {
    <visibility: Visibility?> <name: Name> => {
        let r#type = None;
        Attribute { visibility, name, r#type }
    }
}

Edge: Edge<'input> = {
    <a: Name> "--" <b: Name> => {
        let kind = EdgeKind::Association {
            name: None,
            a: AssociationEnd::default(),
            b: AssociationEnd::default(),
        };
        Edge { a, b, kind }
    }
}

Visibility: Visibility = {
    "-" => Visibility::Private,
    "~" => Visibility::Package,
    "#" => Visibility::Protected,
    "+" => Visibility::Public,
}

Metas: BTreeMap<&'input str, Meta> = {
    "#[" <mut attrs: (<Meta> ",")*> <attr: (<Meta> ","?)> "]" "\n"* => {
        attrs.push(attr);
        BTreeMap::from_iter(attrs.into_iter().map(|attr| (attr.name(), attr)))
    },
    => BTreeMap::new(),
}

Meta: Meta = {
    "pos" "(" <Num> "," <Num> ")" => Meta::Position(<>),
}

ClassifierKind: (ClassifierKind, Option<&'input str>) = {
    "class" => (ClassifierKind::Class, None),
    "dataType" => (ClassifierKind::DataType, None),
    "enumeration" => (ClassifierKind::Enumeration, None),
    "interface" => (ClassifierKind::Interface, None),
    "primitive" => (ClassifierKind::Primitive, None),
    "annotation" => (ClassifierKind::Interface, Some(<>)),
    "exception" => (ClassifierKind::Class, Some(<>)),
    "struct" => (ClassifierKind::Class, Some(<>)),
}

Num: isize = {
    r"[-+]?\d+" =>? isize::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number is too big"
        })
}

Name: &'input str = {
    r"[_\p{ID_Start}][_\p{ID_Continue}-]*"
}

match {
    // don't allow random newlines
    r"[\s&&[^\r\n]]*" => { },
    _
}
