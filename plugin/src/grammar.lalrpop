use std::borrow::Cow;
use std::collections::BTreeMap;

use either::Either;

use crate::model::*;
use super::{from_mark, parse_isize, parse_f32, parse_angle, parse_string};

grammar;

pub Diagram: Diagram<'input> = {
    "\n"* <mut items: (<ClassifierOrEdge> "\n"+)*> <item: ClassifierOrEdge?> => {
        items.extend(item);
        let mut classifiers = Vec::new();
        let mut edges = Vec::new();
        for item in items {
            match item {
                Either::Left(item) => classifiers.push(item),
                Either::Right(item) => edges.push(item),
            }
        }
        Diagram { classifiers, edges }
    }
}

ClassifierOrEdge: Either<Classifier<'input>, Edge<'input>> = {
    Classifier => Either::Left(<>),
    Edge => Either::Right(<>),
}

Classifier: Classifier<'input> = {
    <meta: Metas>
    <is_abstract: "abstract"?> <is_final: "final"?> <kind: ClassifierKind> <name: Name> <id: ("as" <Name>)?> <body: ClassifierBody> => {
        let (kind, stereotype) = kind;
        let is_abstract = is_abstract.is_some() || kind == ClassifierKind::Interface;
        let is_final = is_final.is_some();
        let mut stereotypes = Vec::new();
        stereotypes.extend(stereotype);
        let (attributes, operations) = body;
        Classifier { meta, is_abstract, is_final, kind, name, id, stereotypes, attributes, operations }
    }
}

ClassifierBody: (Vec<Attribute<'input>>, Vec<Operation<'input>>) = {
    ("{" "}")? => (Vec::new(), Vec::new()),
    "{" "\n"+
        <attrs: (<Attribute> "\n"+)*>
        <ops: (<Operation> "\n"+)*>
    "}" => (attrs, ops),
}

Attribute: Attribute<'input> = {
    <visibility: Visibility?> <r#static: "static"?> <name: Name> <r#type: (":" <NameOrString>)?>
    <multiplicity: ("[" <MultiplicityRange> "]")?> <modifiers: Modifiers> => {
        let r#static = r#static.is_some();
        Attribute { visibility, r#static, name, r#type, multiplicity, modifiers }
    }
}

Operation: Operation<'input> = {
    <visibility: Visibility?> <r#abstract: "abstract"?> <name: Name> "(" <parameters: Parameters> ")" <return_type: (":" <NameOrString>)?> => {
        let r#abstract = r#abstract.is_some();
        Operation { visibility, r#abstract, name, parameters, return_type }
    }
}

Parameters: Vec<Parameter<'input>> = {
    => Vec::new(),
    <mut parameters: (<Parameter> ",")*> <parameter: (<Parameter> ","?)> => {
        parameters.push(parameter);
        parameters
    },
}

Parameter: Parameter<'input> = {
    <name: Name> <r#type: (":" <NameOrString>)?> => {
        Parameter { name, r#type }
    }
}

Edge: Edge<'input> = {
    <meta: Metas>
    <a: Name> <kind: EdgeKind> <b: Name> => Edge { meta, a, b, kind }
}

EdgeKind: EdgeKind<'input> = {
    "..|>" => EdgeKind::Realization { direction: Direction::AToB },
    "<|.." => EdgeKind::Realization { direction: Direction::BToA },
    "--|>" => EdgeKind::Generalization { direction: Direction::AToB },
    "<|--" => EdgeKind::Generalization { direction: Direction::BToA },
    "..>" => EdgeKind::Dependency { direction: Direction::AToB, name: None },
    "<.." => EdgeKind::Dependency { direction: Direction::BToA, name: None },
    <a_role: ("(" <Attribute> ")")?> <marks: r"([<x]|[o*]?(-x)?)--((x-)?[o*]?|[x>])"> <b_role: ("(" <Attribute> ")")?>  => {
        let (a, b) = {
            let mut iter = marks.split("--");
            let a = iter.next().unwrap();
            let b = iter.next().unwrap();
            assert!(iter.next().is_none());
            (from_mark(a, a_role), from_mark(b, b_role))
        };
        EdgeKind::Association { name: None, a, b }
    },
}

Visibility: Visibility = {
    "-" => Visibility::Private,
    "~" => Visibility::Package,
    "#" => Visibility::Protected,
    "+" => Visibility::Public,
}

MultiplicityRange: MultiplicityRange = {
    Multiplicity => MultiplicityRange::Exact(<>),
    <Multiplicity> ".." <Multiplicity> => MultiplicityRange::Range(<>),
}

Multiplicity: Multiplicity = {
    Int => Multiplicity::Number(<>),
    "*" => Multiplicity::Any,
}

Modifiers: Vec<Modifier<'input>> = {
    => Vec::new(),
    "{" <mut modifiers: (<Modifier> ",")*> <modifier: Modifier> ","? "}" => {
        modifiers.push(modifier);
        modifiers
    },
}

Modifier: Modifier<'input> = {
    "id" => Modifier::Id,
    "readOnly" => Modifier::ReadOnly,
    "ordered" => Modifier::Ordered,
    "unique" => Modifier::Unique,
    "nonunique" => Modifier::Nonunique,
    "seq" => Modifier::Sequence,
    "sequence" => Modifier::Sequence,
    "union" => Modifier::Union,
    "redefines" <Name> => Modifier::Redefines(<>),
    "subsets" <Name> => Modifier::Subsets(<>),
    String => Modifier::Constraint(<>),
}

Metas: BTreeMap<&'input str, Meta> = {
    "#[" "\n"* <mut attrs: (<Meta> "," "\n"*)*> <attr: (<Meta> ","? "\n"*)> "]" "\n"* => {
        attrs.push(attr);
        BTreeMap::from_iter(attrs.into_iter().map(|attr| (attr.name(), attr)))
    },
    => BTreeMap::new(),
}

Meta: Meta = {
    "pos" "(" <Float> "," <Float> ")" => Meta::Position(<>),
    "via" "(" <mut points: ("(" <Float> "," <Float> ")" ",")*> <point: ("(" <Float> "," <Float> ")" ","?)> ")" => {
        points.push(point);
        Meta::Via(points)
    },
    "bend" "(" <Angle> ")" => Meta::Bend(<>),
}

ClassifierKind: (ClassifierKind, Option<&'input str>) = {
    "class" => (ClassifierKind::Class, None),
    "dataType" => (ClassifierKind::DataType, None),
    "enumeration" => (ClassifierKind::Enumeration, None),
    "interface" => (ClassifierKind::Interface, None),
    "primitive" => (ClassifierKind::Primitive, None),
    "annotation" => (ClassifierKind::Interface, Some(<>)),
    "exception" => (ClassifierKind::Class, Some(<>)),
    "struct" => (ClassifierKind::Class, Some(<>)),
}

NameOrString: Cow<'input, str> = {
    Name => Cow::from(<>),
    String,
}

Name: &'input str = {
    r"[_\p{ID_Start}][_\p{ID_Continue}-]*"
}

Int: isize = {
    r"[-+]?\d+" =>? parse_isize(<>),
}

Float: f32 = {
    r"[-+]?\d+" =>? parse_f32(<>),
    r"[-+]?\d+\.\d+" =>? parse_f32(<>),
}

Angle: f32 = {
    r"[-+]?\d+(\.\d+)?(rad|deg)" =>? parse_angle(<>),
}

String: Cow<'input, str> = {
    r#""[^"\\\r\n]*""# => parse_string(<>)
}

match {
    r"[\s&&[^\r\n]]*" => { }, // don't allow random newlines
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    r"([<x]|[o*]?(-x)?)--((x-)?[o*]?|[x>])",
} else {
    r"[_\p{ID_Start}][_\p{ID_Continue}-]*",
    _
}
